Execute (setting up test environment and helpers):
  function! TestSetup(...) abort
    call mkdir('tmp-test', 'p')
    chdir tmp-test
    call TestWith(get(a:000, 0, []))
  endfunction

  function! TestWith(paths) abort
    let root = getcwd()
    for path in a:paths
      let abs_path      = root . '/' . path
      let parent_dir    = fnamemodify(abs_path, ':h')
      if path =~? '.::.'
        let assume_exists = path =~? '^!'
        let [from, to]    = split(path[(assume_exists):], '::')
        let abs_from      = root . '/' . from
        let abs_to        = root . '/' . to
        let abs_to_dir    = fnamemodify(substitute(abs_to, '/$', '', ''), ':h')

        if !assume_exists
          call TestWith([from])
        endif

        call mkdir(abs_to_dir, 'p')
        call system('ln -s ' . shellescape(abs_from) . ' ' . shellescape(abs_to))
      elseif path =~? '*$'
        call mkdir(parent_dir, 'p')
        call writefile([], abs_path[:-2], 'b')
        call system('chmod +x ' . shellescape(abs_path[:-2]))
      elseif path =~? '/$'
        call mkdir(abs_path, 'p')
      else
        call mkdir(parent_dir, 'p')
        call writefile([], abs_path, 'b')
      endif
    endfor
    Treevial!
  endfunction

  function! TestTeardown() abort
    chdir! -
    call delete('tmp-test', 'rf')
  endfunction

Before:
  call TestSetup([
    \ 'example.txt',
    \ 'example.sh*',
    \ 'example.sh::example.ln',
    \ '!broken::broken.ln',
    \ 'folded-dir/directory/',
    \ 'folded-file/file/example.txt',
    \ '!folded-dir/::folded-sym',
    \ 'expandable/1.txt',
    \ 'expandable/2.txt',
    \ 'expandable/deep/3.txt',
    \ 'expandable/deep/4.txt'
    \ ])

After:
  call TestTeardown()

================================================================================

Execute (buffer has name "treevial"):
  Assert bufexists('treevial')

================================================================================

Execute (buffer has filetype "treevial"):
  AssertEqual 'treevial', getbufvar('treevial', '&filetype')

================================================================================

Execute (buffer is readonly):
  AssertEqual 1, getbufvar('treevial', '&readonly')

================================================================================

Expect (buffer lists contents):
  tmp-test/
  + expandable/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Do (creating a selection using <Tab> shows selection marks):
  ggj\<Tab>

Expect (buffer shows selection marks):
  tmp-test/
  + expandable/
  • folded-dir/directory/
  • folded-file/file/example.txt
  • folded-sym/directory/
  • broken.ln
  • example.ln
  • example.sh
  • example.txt

================================================================================

Do (unmark using <u> hides selection marks):
  ggj\<Tab>u

Expect (buffer no longer shows selection marks):
  tmp-test/
  + expandable/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Do (expand a directory with <enter>):
  ggj\<Cr>

Expect (buffer shows expanded directory):
  tmp-test/
  - expandable/
    + deep/
      1.txt
      2.txt
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Do (create a new file):
  ggc/nested/directory/c.txt\<Cr>

Then (newly created file exists):
  Assert filereadable('nested/directory/c.txt')

Expect (buffer shows newly created file):
  tmp-test/
  + expandable/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    nested/directory/c.txt
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Do (create a new directory):
  ggc/directory/\<Cr>

Then (newly created file exists):
  Assert isdirectory('directory')

Expect (buffer shows newly created file):
  tmp-test/
    directory/
  + expandable/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Do (rename a directory):
  ggjm\<Bs>-renamed\<Cr>

Then (target is directory):
  Assert isdirectory('expandable-renamed')
  Assert !isdirectory('expandable')

Expect (buffer shows renamed directory):
  tmp-test/
  + expandable-renamed/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Execute (stub confirm answer for delete):
  let g:__treevial_vader_confirm_reply__ = 2

Do (delete a directory):
  ggjd

Then (directory is deleted):
  Assert !isdirectory('expandable')

Expect (buffer no longer shows deleted directory):
  tmp-test/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt

================================================================================

Execute (stub confirm answer for delete):
  let g:__treevial_vader_confirm_reply__ = 1

Do (delete a directory):
  ggjd

Then (directory is not deleted):
  Assert isdirectory('expandable')

Expect (buffer shows directory):
  tmp-test/
  + expandable/
    folded-dir/directory/
    folded-file/file/example.txt
    folded-sym/directory/
    broken.ln
    example.ln
    example.sh
    example.txt
